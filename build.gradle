plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.10.1"
    id 'net.researchgate.release' version '2.6.0'
}

version = '0.1-SNAPSHOT'
group = "anverus.tools"

repositories {
    jcenter()
}

dependencies {
    implementation 'io.prometheus:simpleclient_pushgateway:0.6.0'
    implementation 'com.google.guava:guava:27.1-jre'

    testImplementation 'org.spockframework:spock-core:1.2-groovy-2.5'
}

gradlePlugin {
    plugins {
        buildTimingPlugin {
            id = 'anverus.tools.timer'
            implementationClass = 'anverus.tools.gradle.timer.BuildTimingPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/anverus/gradle-build-timer'
    vcsUrl = 'https://github.com/anverus/gradle-build-timer'
    description = 'Collects gradle build timing information and reports it to various sources'
    tags = ['timer', 'monitoring']

    plugins {
        buildTimingPlugin {
            displayName = 'Gradle Build Time Monitoring plugin'
        }
    }
}

sourceCompatibility = 8
targetCompatibility = 8

publishing {
    repositories {
        maven {
            credentials {
                username "${project.findProperty('maven.repo.user')}"
                password "${project.findProperty('maven.repo.password')}"
            }

            if(project.version.endsWith('-SNAPSHOT')) {
                url "${project.findProperty('maven.repo.snapshot.url')}"
            } else {
                url "${project.findProperty('maven.repo.release.url')}"
            }
        }
    }
}