plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'idea'
    id 'jacoco'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.10.1"
    id 'net.researchgate.release' version '2.6.0'
    id "com.jfrog.bintray" version "1.8.4"
}

group = "anverus.tools"

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    implementation 'io.prometheus:simpleclient_pushgateway:0.6.0'
    implementation 'com.google.guava:guava:27.1-jre'

    testImplementation 'org.spockframework:spock-core:1.2-groovy-2.5'
    testCompile gradleTestKit()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.jacocoTestReport.dependsOn test
tasks.check.dependsOn jacocoTestReport

jacoco {
    toolVersion = '0.8.3'
}

def githubProjectUrl = 'https://github.com/anverus/gradle-build-timer'
pluginBundle {
    website = githubProjectUrl
    vcsUrl = githubProjectUrl
    description = 'Collects gradle build timing information and reports it to various sources'
    tags = ['timer', 'monitoring']

    plugins {
        buildTimingPlugin {
            displayName = 'Gradle Build Time Monitoring plugin'
        }
    }
}

gradlePlugin {
    plugins {
        buildTimingPlugin {
            id = 'anverus.tools.timer'
            implementationClass = 'anverus.tools.gradle.timer.BuildTimingPlugin'
        }
    }
}

publishing {
    repositories {
        maven {
            credentials {
                username "${project.findProperty('maven.repo.user')}"
                password "${project.findProperty('maven.repo.password')}"
            }

            if(project.version.endsWith('-SNAPSHOT')) {
                url "${project.findProperty('maven.repo.snapshot.url')}"
            } else {
                url "${project.findProperty('maven.repo.release.url')}"
            }
        }
    }
}

if (project.hasProperty('bintray.username') && project.hasProperty('bintray.api.key')) {
    bintray {
        user = project.findProperty('bintray.username')
        key = project.findProperty('bintray.api.key')

        configurations = ['archives']
        pkg {
            repo = 'maven'
            name = 'gradle-build-timer'
            websiteUrl = githubProjectUrl
            vcsUrl = githubProjectUrl
            githubRepo = 'anverus/gradle-build-timer'
            githubReleaseNotesFile = 'README.md'
            licenses = ['MIT']
            publish = true
            version {
                name = project.version
                released = new Date()
            }
        }
    }

    afterReleaseBuild.dependsOn bintrayUpload
}
afterReleaseBuild.dependsOn publishPlugins